// 1.1 配置是否为application
if (PERSONALCENTER_IS_APPLICATION.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    //配置统一的版本
    compileSdkVersion COMPILER_SDK_VERSION.toInteger()
    buildToolsVersion BUILD_TOOLS_VERSION


    defaultConfig {
        if (PERSONALCENTER_IS_APPLICATION.toBoolean()) {
            applicationId "com.example.personalcenter"
        }

        //配置统一的版本
        minSdkVersion MIN_SDK_VERSION.toInteger()
        targetSdkVersion TAR_SDK_BERSION.toInteger()
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //arouter配置1
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
//1.3 配置清单文件
    sourceSets {
        main {
            if (PERSONALCENTER_IS_APPLICATION.toBoolean()) {
                //单独运行
                manifest.srcFile('src/main/AndroidManifest.xml')
            } else {
                //不是单独运行
                manifest.srcFile('src/main/manifest/AndroidManifest.xml')
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //引入公共的library
    implementation project(path:':commonbase')
    //arouter配置2
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
